pkgname=seafile
pkgver=4.1.5
pkgrel=3
pkgdesc="Next-generation open source cloud storage with advanced features on privacy protection and teamwork. (Server components, without seahub)"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/haiwen/seafile/"
license=('GPL3')
depends=(
    'libevhtp-seafile'
)
makedepends=('vala' 'intltool')
optdepends=()
provides=()
options=('!libtool')
install=seafile.install
source=(
    "https://github.com/haiwen/seafile/archive/v${pkgver}.tar.gz"
    "seafile.install"
    "seafile.service"
)

build ()
{
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./autogen.sh
    ./configure --enable-server --prefix=/usr PYTHON=/usr/bin/python2
    make -j1
}

package ()
{
    # Install library and header files
    cd "${srcdir}/${pkgname}-${pkgver}"
    make install DESTDIR="${pkgdir}/"

    mkdir -p "${pkgdir}/var/log/seafile"
    install -D -m644 "${srcdir}/seafile.service" "${pkgdir}/usr/lib/systemd/system/seafile.service"
}

md5sums=('16edacaf4b3ec04ec63d7b339bd606b7'
         'd2221fc4815ee322fdbb05fb5e6262b3'
         'a60291af64110ed5986feb9cd8027433')
